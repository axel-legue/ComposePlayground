buildscript {
    ext {
        compose_version = '1.3.0-alpha01'

        // ACCOMPANIST
        accompanistVersion = '0.24.9-beta'

        // HILT
        hiltVersion = '2.40.5'
        hiltNavigationVersion = '1.0.0'

        // TIMBER
        timberVersion = '5.0.1'
    }
}// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.2.1' apply false
    id 'com.android.library' version '7.2.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.0' apply false
    id 'com.google.dagger.hilt.android' version '2.41' apply false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/* https://emmanuelkehinde.io/setting-up-git-pre-commit-pre-push-hook-for-ktlint-check/ */
task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}

/*
 Launch this command to generate compose compiler reports
 ./gradlew assembleRelease -PenableComposeCompilerReports=true --rerun-tasks
 */
subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("enableComposeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
            }
        }
    }
}

tasks.getByPath(':app:preBuild').dependsOn installGitHook

